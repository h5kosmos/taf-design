plugins {
    id 'java'
    id "io.freefair.lombok" version "6.5.1"
    id "org.sonarqube" version "3.4.0.2513"
}

group 'com.taf'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}
ext {
    defaultEncoding = 'UTF-8'
    javaVersion = '17'
    testNgVersion = '7.6.1'
    log4jVersion = '2.19.0'
    reportPortalAgentVersion = '5.1.3'
    reportPortalLoggerVersion = '5.1.5'
    springBootVersion = '2.7.4'
}

compileJava {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    options.encoding = defaultEncoding
}

dependencies {
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4jVersion
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion
    implementation group: 'com.epam.reportportal', name: 'agent-java-testng', version: reportPortalAgentVersion
    implementation group: 'com.epam.reportportal', name: 'logger-java-log4j', version: reportPortalLoggerVersion
    implementation group: 'org.testng', name: 'testng', version: testNgVersion
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter', version: springBootVersion
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion,
            { exclude group: 'com.vaadin.external.google', module: 'android-json' }
}

test {
    doFirst {
        File implementation = createServiceLoaderFile()
        implementation << "com.taf.utils.reportportal.ReportPortalListener"
        systemProperties System.getProperties()
    }
    useTestNG() {
        testLogging {
            events "failed", "standardError"
            exceptionFormat "full"
        }
        useDefaultListeners = false
        suites("src/test/resources/suites/suite.xml")
    }
}

def createServiceLoaderFile() {
    File outputDirectory = file(sourceSets.main.output.resourcesDir.absolutePath + "/META-INF/services")
    outputDirectory.mkdirs()
    File implementation = file(outputDirectory.absolutePath + "/org.testng.ITestNGListener")
    implementation.delete()
    implementation.createNewFile()
    return implementation
}